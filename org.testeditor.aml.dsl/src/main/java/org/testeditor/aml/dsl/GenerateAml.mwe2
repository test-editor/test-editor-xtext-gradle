module org.testeditor.aml.dsl.GenerateAml

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
var fileHeader =
"/*******************************************************************************\n* Copyright (c) 2012 - \${year} Signal Iduna Corporation and others.\n* All rights reserved. This program and the accompanying materials\n* are made available under the terms of the Eclipse Public License v1.0\n* which accompanies this distribution, and is available at\n* http://www.eclipse.org/legal/epl-v10.html\n* \n* Contributors:\n* Signal Iduna Corporation - initial API and implementation\n* akquinet AG\n* itemis AG\n*******************************************************************************/"

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.testeditor.aml.dsl"
				rootPath = rootPath
                genericIde = {
                    enabled = true
                }
				runtimeTest = {
					enabled = true
				}
				web = {
					enabled = true
				}
				mavenLayout = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = fileHeader
			}
		}
		language = StandardLanguage {
			name = "org.testeditor.aml.dsl.Aml"
			fileExtensions = "aml"
			referencedResource = "platform:/resource/org.testeditor.dsl.common.model/model/common.xcore"
			referencedResource = "platform:/resource/org.testeditor.aml.model/model/aml.xcore"
			formatter = {
				generateStub = true
			}
			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
			}
		}
	}
}
